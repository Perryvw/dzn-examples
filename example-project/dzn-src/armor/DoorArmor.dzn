import components/IDoor.dzn;
import external/IDoorExternal.dzn;

import utils/logging.dzn;
import utils/timer.dzn;

namespace example.dezyne.armor {

component DoorArmor
{
    provides IDoor strict;
    requires IDoorExternal unsafe;

    requires utils.ITimer timer;

    requires injected utils.ErrorHelper errorHelper;
    requires injected utils.ILogging logging;

    behavior
    {
        on strict.Open(error):
        {
            if (unsafe.Open(error) == Result.Ok)
            {
                timer.Start($std::chrono::seconds(60)$);
                reply(Result.Ok);
            }
            else
            {
                reply(Result.Error);
            }
        }

        on strict.Close(error):
        {
            if (unsafe.Close(error) == Result.Ok)
            {
                timer.Start($std::chrono::seconds(60)$);
                reply(Result.Ok);
            }
            else
            {
                reply(Result.Error);
            }
        }

        on unsafe.Opened():
        {
            [strict.state.Opening]
            {
                timer.Cancel();
                strict.Opened();
            }
            [otherwise]
            {
                logging.LogWarning($"Unexpected door 'Opened' event"$);
            }
        }

        on unsafe.Closed():
        {
            [strict.state.Closing]
            {
                timer.Cancel();
                strict.Closed();
            }
            [otherwise]
            {
                logging.LogWarning($"Unexpected door 'Closed' event"$);
            }
        }

        on unsafe.Fail(error):
        {
            [strict.state.Opening || strict.state.Closing]
            {
                timer.Cancel();
                strict.Fail(error);
            }
            [otherwise]
            {
                logging.LogWarning($"Unexpected door 'Fail' event"$);
            }
        }

        on timer.Timeout():
        {
            Error error;
            errorHelper.AddMessage(error, $"Timeout waiting for the door hardware"$);
            strict.Fail(error);
        }
    }
}

}
